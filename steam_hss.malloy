// Steam Hardware & Software Survey - https://store.steampowered.com/hwsurvey
// data collection method and comments in https://github.com/myagues/steam-hss-data

source: steam_hss is table('duckdb:steam_hss.parquet') + {
  declare:
    date_ts is cast(`date` as timestamp)

  rename:
    value is `index`

  measure:
    avg_perc is round(avg(perc), 2)
    max_perc is round(max(perc), 2)

  query: by_value is {
    group_by: value
    aggregate: avg_perc
  }
  query: by_category is {
    group_by: category
    aggregate: avg_perc
  }
  query: by_platform is {
    group_by: platform
  }
  query: by_year is {
    group_by: dep_year is left(date_ts::timestamp.year::string, 4)
    order_by: dep_year asc
  }
  query: by_qtr is {
    group_by: dep_qtr is date_ts.quarter
    order_by: dep_qtr asc
  }
  query: list_bar_explorer is {
    nest: by_platform_dashboard is by_platform + {
      nest: top_by_year is by_year + {
        nest: max_perc_list_detail is {
          group_by: value
          aggregate: max_perc
          top: 3
        }
        order_by: 1 desc
      }
      nest: by_year is by_year + {
        nest: top_10_bar_chart is {
          aggregate: avg_perc
          group_by: value
          top: 10
        }
        where: date_ts.year: @2005 | @2010 | @2015 | @2022
        order_by: 1 desc
      }
    }
  }
  query: list_line_explorer is {
    nest: by_platform_dashboard is by_platform + {
      nest: top_by_year is by_year + {
        nest: max_perc_list_detail is {
          group_by: value
          aggregate: max_perc
          top: 1
        }
      }
      nest: by_year_line_chart is by_qtr + {
        aggregate: avg_perc
        group_by: value
      }
    }
  }
}
